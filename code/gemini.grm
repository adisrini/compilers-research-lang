structure GA = GeminiArray
structure BA = BitArray

%%
%term
    EOF
  | DATATYPE | TYPE | VAL | REF | FUN | MODULE | STRUCTURE | STRUCT | SIGNATURE | SIG | LIST
  | LET | IN | END | IF | THEN | ELSE
  | ORELSE | ANDALSO | NOT
  | NIL | WITH | OF | OP | CASE
  | BIT_NOT | BIT_OR | BIT_AND | BIT_XOR | BIT_SLL | BIT_SRL | BIT_SRA
  | GE | GT | LE | LT | NEQ | EQ
  | INT_DIVIDE | INT_TIMES | INT_PLUS | INT_MINUS | INT_MOD | REAL_DIVIDE | REAL_TIMES | REAL_PLUS | REAL_MINUS
  | RBRACE | LBRACE | RBRACK | LBRACK | RPAREN | LPAREN
  | DOT | SEMICOLON | COLON | COMMA | POUND | AT | TICK | ASSIGN | BANG | CONS
  | ID of string | INT of int | STRING of string | REAL of real | BIT of Bit.bit | BIT_ARRAY of BitArray.bit_array
  | UMINUS | APPLY

%nonterm
      program
        | structs_or_sigs
            | structures
                | structure
                    | struct_body
            | signatures
                | signature
                    | sig_body
                    | sigs
                        | val_sig
                        | ty_sig
                        | dataty_sig
                        | module_sig
                        | non_ty_sigs
                        | non_dataty_sigs
                        | non_module_sigs
        | exps
            | exp
                | array_access
                | access
                    | struct_access
                    | tuple_access
                    | record_access
                    | deref
                | operation
                    | int_op
                    | real_op
                    | bit_op
                    | compare_op
                    | list_op
                | let_block
                    | let_body
                        | semicolon_exp0
                    | decs
                        | val_dec
                        | fun_dec
                            | fun_dec_no_type
                            | fun_dec_with_type
                            | fun_params
                                | fun_param
                        | ty_dec
                            | ty
                                | ty_fields
                                | ty_field_tail
                            | with_type
                        | dataty_dec
                            | dataty_tail
                        | module_dec
                            | module_dec_no_type
                            | module_dec_with_type
                        | non_fun_dec
                        | non_ty_dec
                        | non_dataty_dec
                        | non_module_dec
                | exp_seq
                | assign
                | conditional
                | insts
                    | rec_inst
                        | rec_inst_body
                        | rec_field_comma_tail
                    | ref_inst
                    | array_inst
                        | array_index_fn
                        | index_fn_tail
                        | array_from_num
                    | list_inst
                        | list_elements
                    | sw_tuple_inst
                    | hw_tuple_inst
                    | exp_comma_tail
                | with_value
                | pattern_match
        | epsilon

%pos int
%verbose
%start program
%eop EOF
%noshift EOF

%name Gemini

%keyword DATATYPE TYPE VAL REF FUN MODULE STRUCTURE STRUCT SIGNATURE SIG
  LET IN END IF THEN ELSE
  ORELSE ANDALSO NOT
  NIL WITH OF OP

%prefer THEN ELSE LPAREN

%value ID ("foo")
%value INT (1)
%value STRING ("")
%value BIT (Bit.fromInt(0))
%value REAL (1.0)

%change -> IN ID END
      | EQ -> ASSIGN
      | ASSIGN -> EQ
      | SEMICOLON ELSE -> ELSE
      | LET ID EQ -> LET TYPE ID EQ
      | LET ID ASSIGN -> LET VAL ID ASSIGN
      | LET ID COLON ID ASSIGN -> LET VAL ID COLON ID ASSIGN
      | LET ID LPAREN -> LET FUN ID LPAREN
      | COMMA -> SEMICOLON
      | SEMICOLON -> COMMA
      | SEMICOLON -> COLON
      | COLON -> SEMICOLON
      | SEMICOLON THEN -> THEN
      | SEMICOLON VAL -> VAL
      | SEMICOLON TYPE -> TYPE
      | SEMICOLON FUN -> FUN
      | SEMICOLON DATATYPE -> DATATYPE
      | SEMICOLON MODULE -> MODULE
      | SEMICOLON END -> END
      | SEMICOLON IN -> IN

%nonassoc INT STRING REAL BIT ID
%nonassoc OF
%nonassoc THEN
%nonassoc ELSE
%nonassoc LBRACK RBRACK
%right ASSIGN
%right WITH
%left LBRACE RBRACE
%left CONS
%left ORELSE
%left ANDALSO
%left BIT_SLL BIT_SRA BIT_SRL
%nonassoc EQ NEQ GT LT GE LE
%left REAL_MINUS REAL_PLUS INT_MINUS INT_PLUS BIT_XOR BIT_OR
%left REAL_DIVIDE REAL_TIMES INT_DIVIDE INT_TIMES BIT_AND INT_MOD
%left UMINUS BIT_NOT
%left LPAREN RPAREN APPLY

%%

program	      : structs_or_sigs                                             ()
              | exp                                                         ()

exp           : exp exp %prec APPLY                                         ()
              | INT                                                         ()
              | STRING                                                      ()
              | REAL                                                        ()
              | BIT                                                         ()
              | ID                                                          ()
              | access                                                      ()
              | operation                                                   ()
              | let_block                                                   ()
              | assign                                                      ()
              | exp_seq                                                     ()
              | conditional                                                 ()
              | insts                                                       ()
              | with_value                                                  ()
              | pattern_match                                               ()
              | LPAREN RPAREN                                               ()

structs_or_sigs
              : structures structs_or_sigs                                  ()
              | signatures structs_or_sigs                                  ()
              | epsilon                                                     ()

structures    : structure structures                                        ()
              | structure                                                   ()

structure     : STRUCTURE ID EQ struct_body                                 ()
              | STRUCTURE ID COLON ID EQ struct_body                        ()
              | STRUCTURE ID COLON sig_body EQ struct_body                  ()

struct_body   : STRUCT decs END                                             ()

signatures    : signature signatures                                        ()
              | signature                                                   ()

signature     : SIGNATURE ID EQ sig_body                                    ()

sig_body      : SIG sigs END                                                ()

sigs          : val_sig sigs                                                ()
              | ty_sig non_ty_sigs                                          ()
              | module_sig non_module_sigs                                  ()
              | dataty_sig non_dataty_sigs                                  ()
              | epsilon                                                     ()

non_ty_sigs   : val_sig sigs                                                ()
              | module_sig non_module_sigs                                  ()
              | dataty_sig non_dataty_sigs                                  ()
              | epsilon                                                     ()

non_module_sigs
              : val_sig sigs                                                ()
              | ty_sig non_ty_sigs                                          ()
              | dataty_sig non_dataty_sigs                                  ()
              | epsilon                                                     ()

non_dataty_sigs
              : val_sig sigs                                                ()
              | ty_sig non_ty_sigs                                          ()
              | module_sig non_module_sigs                                  ()
              | epsilon                                                     ()

val_sig       : VAL ID COLON ty                                             ()

ty_sig        : TYPE ID EQ ty                                               ()
              | TYPE ID EQ ty ty_sig                                        ()
              | TYPE ID                                                     ()
              | TYPE ID ty_sig                                              ()

module_sig    : MODULE ID COLON ty INT_MINUS GT ty                          ()
              | MODULE ID COLON ty INT_MINUS GT ty module_sig               ()

dataty_sig    : dataty_dec                                                  ()
              | dataty_dec dataty_sig                                       ()
              | DATATYPE ID                                                 ()
              | DATATYPE ID dataty_sig                                      ()

array_access  : exp LBRACK exp RBRACK                                       ()

struct_access : exp DOT ID                                                  ()

tuple_access  : POUND INT exp                                               ()

record_access : POUND ID exp                                                ()

deref         : BANG exp                                                    ()

access        : array_access                                                ()
              | struct_access                                               ()
              | tuple_access                                                ()
              | record_access                                               ()
              | deref                                                       ()

operation     : int_op                                                      ()
              | real_op                                                     ()
              | bit_op                                                      ()
              | compare_op                                                  ()
              | list_op                                                     ()

int_op        : INT_MINUS exp %prec UMINUS                                  ()
              | exp INT_PLUS exp                                            ()
              | exp INT_MINUS exp                                           ()
              | exp INT_TIMES exp                                           ()
              | exp INT_DIVIDE exp                                          ()
              | exp INT_MOD exp                                             ()

real_op       : exp REAL_PLUS exp                                           ()
              | exp REAL_MINUS exp                                          ()
              | exp REAL_TIMES exp                                          ()
              | exp REAL_DIVIDE exp                                         ()

bit_op        : BIT_NOT exp                                                 ()
              | exp BIT_OR exp                                              ()
              | exp BIT_AND exp                                             ()
              | exp BIT_XOR exp                                             ()
              | exp BIT_SLL exp                                             ()
              | exp BIT_SRL exp                                             ()
              | exp BIT_SRA exp                                             ()

compare_op    : exp EQ exp                                                  ()
              | exp NEQ exp                                                 ()
              | exp GT exp                                                  ()
              | exp LT exp                                                  ()
              | exp GE exp                                                  ()
              | exp LE exp                                                  ()

list_op       : exp CONS exp                                                ()

let_block     : LET decs IN let_body END                                    ()

decs          : val_dec decs                                                ()
              | fun_dec non_fun_dec                                         ()
              | ty_dec non_ty_dec                                           ()
              | dataty_dec non_dataty_dec                                   ()
              | module_dec non_module_dec                                   ()
              | epsilon                                                     ()

non_fun_dec   : val_dec decs                                                ()
              | ty_dec non_ty_dec                                           ()
              | dataty_dec non_dataty_dec                                   ()
              | module_dec non_module_dec                                   ()
              | epsilon                                                     ()

non_ty_dec    : val_dec decs                                                ()
              | fun_dec non_fun_dec                                         ()
              | dataty_dec non_dataty_dec                                   ()
              | module_dec non_module_dec                                   ()
              | epsilon                                                     ()

non_dataty_dec: val_dec decs                                                ()
              | ty_dec non_ty_dec                                           ()
              | fun_dec non_fun_dec                                         ()
              | module_dec non_module_dec                                   ()
              | epsilon                                                     ()

non_module_dec: val_dec decs                                                ()
              | fun_dec non_fun_dec                                         ()
              | ty_dec non_ty_dec                                           ()
              | dataty_dec non_dataty_dec                                   ()
              | epsilon                                                     ()

val_dec       : VAL ID EQ exp                                               ()
              | VAL ID COLON ty EQ exp                                      ()

fun_dec       : fun_dec_no_type                                             ()
              | fun_dec_with_type                                           ()
              | fun_dec_no_type fun_dec                                     ()
              | fun_dec_with_type fun_dec                                   ()

fun_dec_no_type
              : FUN ID fun_params EQ exp                                    ()

fun_dec_with_type
              : FUN ID fun_params COLON ty EQ exp                           ()

fun_params    : fun_param fun_params                                        ()
              | fun_param                                                   ()

fun_param     : LPAREN ID RPAREN                                            ()
              | ID                                                          ()
              | LPAREN ty_fields RPAREN                                     ()

ty_dec        : TYPE ID EQ ty with_type                                     ()
              | TYPE ID EQ ty with_type ty_dec                              ()

ty            : ty INT_TIMES ty                                             ()
              | LBRACE ty_fields RBRACE                                     ()
              | ty AT INT                                                   ()
              | ty LBRACK INT RBRACK                                        ()
              | ty LBRACK RBRACK                                            ()
              | ty LIST                                                     ()

ty_fields     : ty_field_tail                                               ()
              | epsilon                                                     ()

ty_field_tail : ID COLON ty                                                 ()
              | ty_field_tail COMMA ID COLON ty                             ()

with_type     : WITH OP EQ LPAREN ID COMMA ID RPAREN EQ exp                 ()
              | WITH OP NEQ LPAREN ID COMMA ID RPAREN EQ exp                ()
              | WITH OP GT LPAREN ID COMMA ID RPAREN EQ exp                 ()
              | WITH OP LT LPAREN ID COMMA ID RPAREN EQ exp                 ()
              | WITH OP GE LPAREN ID COMMA ID RPAREN EQ exp                 ()
              | WITH OP LE LPAREN ID COMMA ID RPAREN EQ exp                 ()

dataty_dec    : DATATYPE ID EQ ID OF ty dataty_tail                         ()

dataty_tail   : BIT_OR ID OF ty dataty_tail                                 ()
              | epsilon                                                     ()

module_dec    : module_dec_no_type                                          ()
              | module_dec_with_type                                        ()

module_dec_no_type
              : MODULE ID fun_param EQ exp                                  ()

module_dec_with_type
              : MODULE ID fun_param COLON ty EQ exp                         ()

let_body      : exp semicolon_exp0                                          ()

semicolon_exp0: SEMICOLON exp semicolon_exp0                                ()
              | epsilon                                                     ()

exp_seq       : LPAREN exp RPAREN                                           ()
              | LPAREN exp SEMICOLON exp semicolon_exp0 RPAREN              ()

assign        : exp ASSIGN exp                                              ()

exp_comma_tail: COMMA exp exp_comma_tail                                    ()
              | epsilon                                                     ()

conditional   : IF exp THEN exp ELSE exp                                    ()
              | IF exp THEN exp                                             ()
              | exp ANDALSO exp                                             ()
              | exp ORELSE exp                                              ()

insts         : rec_inst                                                    ()
              | ref_inst                                                    ()
              | array_inst                                                  ()
              | list_inst                                                   ()
              | sw_tuple_inst                                               ()
              | hw_tuple_inst                                               ()

rec_inst      : LBRACE rec_inst_body RBRACE                                 ()

rec_inst_body : ID EQ exp rec_field_comma_tail                              ()
              | epsilon                                                     ()

rec_field_comma_tail
              : COMMA ID EQ exp rec_field_comma_tail                        ()
              | epsilon                                                     ()

ref_inst      : REF exp                                                     ()

array_inst    : POUND LBRACK exp exp_comma_tail RBRACK                      ()
              | POUND LBRACK exp COMMA array_index_fn RBRACK                ()
              | BIT_ARRAY                                                   ()

array_index_fn: ID EQ GT exp index_fn_tail                                  ()
              | INT EQ GT exp index_fn_tail                                 ()

index_fn_tail : BIT_OR ID EQ GT exp index_fn_tail                           ()
              | BIT_OR INT EQ GT exp index_fn_tail                          ()
              | epsilon                                                     ()

list_inst     : LBRACK list_elements RBRACK                                 ()
              | NIL                                                         ()

list_elements : exp exp_comma_tail                                          ()
              | epsilon                                                     ()

sw_tuple_inst : LPAREN exp exp_comma_tail RPAREN                            ()

hw_tuple_inst : POUND LPAREN exp exp_comma_tail RPAREN                      ()

with_value    : exp WITH rec_inst                                           ()

pattern_match : CASE                                                        ()

epsilon       :                                                             ()
